program navier_stokes_1D
    !=====================================================================!
    ! Main preamble                                                       !
    !=====================================================================!
    use precision_m                                       
    use navier_stokes_library_1d   
    implicit none 
    integer, parameter                              :: M = 2048
    real(WP)                                        :: dy, dt, nu
    real(WP), dimension(0:M)                        :: v, vstar
    real(WP), dimension(0:M+1)                      :: u , unew
    real(WP)                                        :: ut, ub, vt, vb, v1, v2
    real(WP), dimension(0:M+1)                      :: p, pold
    real(WP)                                        :: t, tfinal
    real(WP)                                        :: rdy, rdy2
    real(WP)                                        :: gs_error, gs_max
    integer                                         :: counter, iter_max, n_gs
    integer                                         :: j
    !---------------------------------------------------------------------!
    ! Domain variables                                                    !
    !---------------------------------------------------------------------!
    dy      = 1.0_WP/dble(M) 
    rdy     = 1.0_WP/dy
    rdy2    = 1.0_WP/(dy)**2.0_WP
    nu      = 0.005_WP
    dt      = 0.25_WP*dy**2.0_WP/nu
    t       = 0.0_WP
    tfinal  = 30.0_WP 
    !---------------------------------------------------------------------!
    ! Wall velocities                                                     !
    !---------------------------------------------------------------------!
    ut      = 1.0_WP
    ub      = 0.0_WP
    vt      = 0.0_WP
    vb      = 0.0_WP
    !---------------------------------------------------------------------!
    ! Preallocating variables                                             !
    !---------------------------------------------------------------------!
    u       = 0.0_WP
    unew    = 0.0_WP
    v       = 0.0_WP
    vstar   = 0.0_WP
    p       = 0.0_WP
    pold    = 0.0_WP
    !---------------------------------------------------------------------!
    ! Setting boundary conditions                                         !
    !---------------------------------------------------------------------!
    u(0)    = 2.0_WP*ub - u(1)
    u(M+1)  = 2.0_WP*ut - u(M)
    v(0)    = vb
    v(M)    = vt
    !---------------------------------------------------------------------!
    ! Convergence criteria                                                !
    !---------------------------------------------------------------------!
    counter     = 0
    gs_max      = 1e-08
    iter_maX    = 2000
    !---------------------------------------------------------------------!
    ! Time loop                                                           !
    !---------------------------------------------------------------------!
    do while (t < tfinal)
        !-----------------------------------------------------------------!
        ! Updating time values                                            !
        !-----------------------------------------------------------------!
        t       = t + dt
        counter = counter + 1
        !-----------------------------------------------------------------!
        ! Calculating u                                                   !
        !-----------------------------------------------------------------!
        call u_time_derv(unew, M, u, v, dt, dy, nu)
        u       = unew
        u(0)    = 2.0_WP*ub - u(1)
        u(M+1)  = 2.0_WP*ut - u(M)
        !-----------------------------------------------------------------!
        ! Calculating vstar                                               !
        !-----------------------------------------------------------------!
        do j = 1, M-1
            v1          = 0.5_WP*(v(j) + v(j+1))
            v2          = 0.5_WP*(v(j) + v(j-1))
            vstar(j)    = v(j) - dt*rdy*(v1**2.0_WP - v2**2.0_WP) &
                                + dt*rdy2*nu*(v(j+1)-2.0_WP*v(j)+v(j-1))
        end do
        !-----------------------------------------------------------------!
        ! vstar boundary conditions                                       !
        !-----------------------------------------------------------------!
        vstar(0)    = vb
        vstar(M)    = vt
        !-----------------------------------------------------------------!
        ! Calculating the pressure                                        !
        !-----------------------------------------------------------------!
        call pressure_calc(p, n_gs, gs_error, M, pold, vstar, &
                                dy, dt, gs_max, iter_max)
        pold    = p
        !-----------------------------------------------------------------!
        ! Calculating v^{n+1}                                             !
        !-----------------------------------------------------------------!
        do j = 1, M-1
            v(j) = vstar(j) - dt*rdy*(P(j+1)-P(j))
        end do
        v(0)    = vb
        v(M)    = vt
        !-----------------------------------------------------------------!
        ! Print statement                                                 !
        !-----------------------------------------------------------------!
        if (counter > 1000) then
            print '(A, ES18.6)', 'time --> ', t
            print '(A, ES18.6)', 'max u --> ', maxval(abs(u))
            print '(A, ES18.6)', 'max v --> ', maxval(abs(v))
            counter = 0
        end if 
    end do
end program
