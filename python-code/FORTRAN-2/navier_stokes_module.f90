module ns_lib
    !=====================================================================!
    ! Preamble                                                            !
    !=====================================================================!
    use precision_m
    implicit none
    public :: linspace, time_derv, calcpress, divergence, u_write, &
                v_write, Lv_write, Lu_write, Ny_write, Nx_write, P_write
    !=====================================================================!
    ! Subroutines                                                         !
    !=====================================================================!
    contains
        !=================================================================!
        ! Creates a 1-D dimensional array with evenly spaced elements     !
        !=================================================================!
        subroutine linspace(x, xstart, xend, xlen)
            !-------------------------------------------------------------!
            ! linspace preamble                                           !
            !-------------------------------------------------------------!
            real(WP), dimension(:), intent(out) :: x
            real(WP), intent(in)                :: xstart, xend
            integer                             :: xlen
            real(WP)                            :: dx
            integer                             :: i       
            !-------------------------------------------------------------!
            ! Generating x output                                         !
            !-------------------------------------------------------------!
            dx          = (xend - xstart)/(xlen-1)              ! spatial step size
            x(1:xlen)   = [(xstart + ((i-1)*dx), i=1, xlen)]    ! x array
        end subroutine
        !=================================================================!
        ! Time step subroutine                                            !
        !=================================================================!
        subroutine time_derv(Lu, Lv, Nx, Ny, M, N, dx, dy, nu, u, v, ul, &
                                ur, ub, ut, vl, vr, vb, vt)
            !-------------------------------------------------------------!
            ! time derivative preamble                                    !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), intent(in)                            :: dx, dy, nu
            real(WP), dimension(:), intent(in)              :: ul, ur, vl, vr
            real(WP), dimension(:), intent(in)              :: ub, ut, vb, vt
            real(WP), dimension(0:N, 0:M-1), intent(in)     :: u
            real(WP), dimension(0:N-1,0:M), intent(in)      :: v
            real(WP)                                        :: dx2, dy2
            real(WP), dimension(0:N, 0:M-1), intent(out)    :: Lu
            real(WP), dimension(0:N, 0:M-1), intent(out)    :: Nx
            real(WP), dimension(0:N-1, 0:M), intent(out)    :: Lv
            real(WP), dimension(0:N-1, 0:M), intent(out)    :: Ny
            integer                                         :: i, j
            !-------------------------------------------------------------!
            ! Domain variables                                            !
            !-------------------------------------------------------------!
            dx2 = dx*dx                 ! spatial step size
            dy2 = dy*dy                 ! spatial step size
            Lu  = 0.0_WP                ! linear u-velocity         i.e., laplacian(u)
            Lv  = 0.0_WP                ! linear v-velocity         i.e., laplacian(v)
            Nx  = 0.0_WP                ! non-linear u-velocity     i.e., u dot grad(u)
            Ny  = 0.0_WP                ! non-linear v-velocity     i.e., u dot grad(v)
            !=============================================================!
            ! Linear terms for u-velocity                                 !
            !=============================================================!
            !-------------------------------------------------------------!
            ! Lower left wall                                             !
            !-------------------------------------------------------------!
            Lu(1,1)     = nu*(u(1,2)-2.0_WP*u(1,1)+ul(1))/dx2 
            Lu(1,1)     = Lu(1,1) + nu*(u(2,1)-3.0_WP*u(1,1)+2.0_WP*ub(1))/dy2     
            !-------------------------------------------------------------!
            ! Bottom wall                                                 !
            !-------------------------------------------------------------!
            do i = 2, M-2
                Lu(1,i)    = nu*(u(1,i+1)-2.0_WP*u(1,i)+u(1,i-1))/dx2
                Lu(1,i)    = Lu(1,i) + nu*(u(2,i)-3.0_WP*u(1,i)+2.0_WP*ub(i))/dy2
            end do
            !-------------------------------------------------------------!
            ! Lower right wall                                            !
            !-------------------------------------------------------------!
            Lu(1,M-1)   = nu*(ur(1)-2.0_WP*u(1,M-1)+u(1,M-2))/dx2
            Lu(1,M-1)   = Lu(1,M-1) + nu*(u(2,M-1)-3.0_WP*u(1,M-1)+2.0_WP*ub(M-1))/dy2
            !-------------------------------------------------------------!
            ! Interior domain                                             !
            !-------------------------------------------------------------!
            do j = 2, N-1
                !---------------------------------------------------------!
                ! Left wall                                               !
                !---------------------------------------------------------!
                Lu(j,1)    = nu*(u(j,2)-2.0_WP*u(j,1)+ul(j))/dx2
                Lu(j,1)    = Lu(j,1) + nu*(u(j+1,1)-2.0_WP*u(j,1)+u(j-1,1))/dy2
                !---------------------------------------------------------!
                ! Interior                                                !
                !---------------------------------------------------------!
                do i = 2, M-2
                    Lu(j,i)    = nu*(u(j,i+1)-2.0_WP*u(j,i)+u(j,i-1))/dx2
                    Lu(j,i)    = Lu(j,i) + nu*(u(j+1,i)-2.0_WP*u(j,i)+u(j-1,i))/dy2
                end do
                !---------------------------------------------------------!
                ! Right wall                                              !
                !---------------------------------------------------------!
                Lu(j,M-1)  = nu*(ur(j)-2.0_WP*u(j,M-1)+u(j,M-2))/dx2
                Lu(j,M-1)  = Lu(j,M-1) + nu*(u(j+1,M-1)-2.0_WP*u(j,M-1)+u(j-1,M-1))/dy2
            end do
            !-------------------------------------------------------------!
            ! Upper left wall                                             !
            !-------------------------------------------------------------!
            Lu(N,1) = nu*(u(N,2)-2.0_WP*u(N,1)+ul(N))/dx2
            Lu(N,1) = Lu(N,1) +  nu*(2.0_WP*ut(1)-3.0_WP*u(N,1)+u(N-1,1))/dy2
            !-------------------------------------------------------------!
            ! Top wall                                                    !
            !-------------------------------------------------------------!
            do i = 2, M-2
                Lu(N,i)    = nu*(u(N,i+1)-2.0_WP*u(N,i)+u(N,i-1))/dx2
                Lu(N,i)    = Lu(N,i) +  nu*(2.0_WP*ut(i)-3.0_WP*u(N,i)+u(N-1,i))/dy2
            end do
            !-------------------------------------------------------------!
            ! Upper right wall                                            !
            !-------------------------------------------------------------!
            Lu(N,M-1)   = nu*(ur(N)-2.0_WP*u(N,M-1)+u(N,M-2))/dx2
            Lu(N,M-1)   = Lu(N,M-1) + nu*(2.0_WP*ut(M-1)-3.0_WP*u(N,M-1)+u(N-1,M-1))/dy2
            !=============================================================!
            ! Linear terms for v-velocity                                 !
            !=============================================================!
            !-------------------------------------------------------------!
            ! Lower left                                                  !
            !-------------------------------------------------------------!
            Lv(1,1) = nu*(v(1,2)-3.0_WP*v(1,1)+2.0_WP*vl(1))/dx2
            Lv(1,1) = Lv(1,1) + nu*(v(2,1)-2.0_WP*v(1,1)+vb(1))/dy2
            !-------------------------------------------------------------!
            ! Bottom wall                                                 !
            !-------------------------------------------------------------!
            do i  = 2, M-1
                Lv(1,i) = nu*(v(1,i+1)-2.0_WP*v(1,i)+v(1,i-1))/dx2
                Lv(1,i) = Lv(1,i) + nu*(v(2,i)-2.0_WP*v(1,i)+vb(i))/dy2
            end do
            !-------------------------------------------------------------!
            ! Lower right                                                 !
            !-------------------------------------------------------------!
            Lv(1,M) = nu*(2.0_WP*vr(1)-3.0_WP*v(1,M)+v(1,M-1))/dx2
            Lv(1,M) = Lv(1,M) +  nu*(v(2,M)-2.0_WP*v(1,M)+vb(M))/dy2
            !-------------------------------------------------------------!
            ! Interior points                                             !
            !-------------------------------------------------------------!
            do j  = 2, N-2
                !---------------------------------------------------------!
                ! Left wall                                               !
                !---------------------------------------------------------!
                Lv(j,1) = nu*(v(j,2)-3.0_WP*v(j,1)+2.0_WP*vl(j))/dx2
                Lv(j,1) = Lv(j,1) +  nu*(v(j+1,1)-2.0_WP*v(j,1)+v(j-1,1))/dy2
                !---------------------------------------------------------!
                ! interior points                                         !
                !---------------------------------------------------------!
                do i = 2, M-1
                    Lv(j,i) = nu*(v(j,i+1)-2.0_WP*v(j,i)+v(j,i-1))/dx2
                    Lv(j,i) = Lv(j,i) +  nu*(v(j+1,i)-2.0_WP*v(j,i)+v(j-1,i))/dy2
                end do
                !---------------------------------------------------------!
                ! Right wall                                              !
                !---------------------------------------------------------!
                Lv(j,M) = nu*(2.0_WP*vr(j)-3.0_WP*v(j,M)+v(j,M-1))/dx2
                Lv(j,M) = Lv(j,M) +  nu*(v(j+1,M)-2.0_WP*v(j,M)+v(j-1,M))/dy2
            end do
            !-------------------------------------------------------------!
            ! Upper left wall                                             !
            !-------------------------------------------------------------!
            Lv(N-1,1) = nu*(v(N-1,2)-3.0_WP*v(N-1,1)+2.0_WP*vl(N-1))/dx2
            Lv(N-1,1) = Lv(N-1,1) + nu*(vt(1)-2.0_WP*v(N-1,1)+v(N-2,1))/dy2
            !-------------------------------------------------------------!
            ! Top wall                                                    !
            !-------------------------------------------------------------!
            do i = 2, M-1
                Lv(N-1,i) = nu*(v(N-1,i+1)-2.0_WP*v(N-1,i)+v(N-1,i-1))/dx2
                Lv(N-1,i) = Lv(N-1,i) + nu*(vt(i)-2.0_WP*v(N-1,i)+v(N-2,i))/dy2
            end do
            !-------------------------------------------------------------!
            ! Upper right wall                                            !
            !-------------------------------------------------------------!
            Lv(N-1,M) = nu*(2.0_WP*vr(N-1)-3.0_WP*v(N-1,M)+v(N-1,M-1))/dx2
            Lv(N-1,M) = Lv(N-1,M) + nu*(vt(M)-2.0_WP*v(N-1,M)+v(N-2,M))/dy2
            !=============================================================!
            ! Non-linear terms for u-velocity                             !
            !=============================================================!
            !-------------------------------------------------------------!
            ! looping over domain                                         !
            !-------------------------------------------------------------!
            do j = 1, N
                !---------------------------------------------------------!
                ! left wall                                               !
                !---------------------------------------------------------!
                Nx(j,1) = (0.25_WP*(u(j,2)+u(j,1))**2.0_WP - &
                            0.25_WP*(u(j,1)+ul(j))**2.0_WP)/dx
                !---------------------------------------------------------!
                ! interior domain                                         !
                !---------------------------------------------------------!
                do i = 2, M-2
                    Nx(j,i) = (0.25_WP*(u(j,i+1) + u(j,i))**2.0_WP - &
                                0.25_WP*(u(j,i) + u(j,i-1))**2.0_WP)/dx
                end do
                !---------------------------------------------------------!
                ! right wall                                              !
                !---------------------------------------------------------!
                Nx(j,M-1) = (0.25_WP*(ur(j)+u(j,M-1))**2.0_WP - & 
                                0.25_WP*(u(j,M-1) + u(j,M-2))**2.0_WP)/dx
            end do
            !-------------------------------------------------------------!
            ! bottom wall                                                 !
            !-------------------------------------------------------------!
            do i = 1, M-1
                Nx(1,i) = Nx(1,i) + (0.25_WP*(u(2,i)+u(1,i))*(v(1,i+1)+v(1,i)) - &
                            0.5_WP*(ub(i))*(vb(i)+vb(i+1)))/dy
            end do
            !-------------------------------------------------------------!
            ! interior domain                                             !
            !-------------------------------------------------------------!
            do j = 2, N-1
                do i = 1, M-1
                    Nx(j,i) = Nx(j,i) +  (0.25_WP*(u(j+1,i)+u(j,i))*(v(j,i+1)+v(j,i)) -&
                                0.25_WP*(u(j,i)+u(j-1,i))*(v(j-1,i)+v(j-1,i+1)))/dy
                end do
            end do 
            !-------------------------------------------------------------!
            ! top wall                                                    !
            !-------------------------------------------------------------!
            do i = 1, M-1
                Nx(N,i) = Nx(N,i) + (0.5_WP*(ut(i))*(vt(i+1)+vt(i)) - &
                            0.25_WP*(u(N,i)+u(N-1,i))*(v(N-1,i)+v(N-1,i+1)))/dy
            end do
            !=============================================================!
            ! Non-linear terms for v-velocity                             !
            !=============================================================!
            !-------------------------------------------------------------!
            ! Left wall                                                   !
            !-------------------------------------------------------------!
            do i = 1, M
                Ny(1,i) = (0.25_WP*(v(2,i)+v(1,i))**2.0_WP - &
                                0.25_WP*(v(1,i)+vb(i))**2.0_WP)/dy
            end do
            !-------------------------------------------------------------!
            ! Interior points dv/dy                                       !
            !-------------------------------------------------------------!
            do j = 2, N-2
                do i = 1, M
                    Ny(j,i) = (0.25_WP*(v(j+1,i) + v(j,i))**2.0_WP - &
                                    0.25_WP*(v(j,i) + v(j-1,i))**2.0_WP)/dy
                end do
            end do
            !-------------------------------------------------------------!
            ! Top wall                                                    ! 
            !-------------------------------------------------------------!
            do i = 1, M
                Ny(N-1,i) = (0.25_WP*(vt(i)+v(N-1,i))**2.0_WP - &
                                0.25_WP*(v(N-1,i)+v(N-2,i))**2.0_WP)/dy
            end do
            !-------------------------------------------------------------!
            ! Interior points dv/dx                                       ! 
            !-------------------------------------------------------------!
            do j = 1, N-1
                !---------------------------------------------------------!
                ! Left domain                                             ! 
                !---------------------------------------------------------!
                Ny(j,1) = Ny(j,1) + (0.25_WP*(u(j,1)+u(j+1,1))*(v(j,2)+v(j,1)) - &
                                0.5_WP*(ul(j)+ul(j+1))*(vl(j)))/dx
                !---------------------------------------------------------!
                ! Interior domain                                         ! 
                !---------------------------------------------------------!
                do i = 2, M-1
                    Ny(j,i) = Ny(j,i) + (0.25*(u(j,i)+u(j+1,i))*(v(j,i+1)+v(j,i)) -&
                                0.25*(u(j,i-1)+u(j+1,i-1))*(v(j,i)+v(j,i-1)))/dx
                end do
                !---------------------------------------------------------!
                ! Right wall domain                                       ! 
                !---------------------------------------------------------!
                Ny(j,M) = Ny(j,M) +  (0.5*(ur(j)+ur(j+1))*(vr(j)) - &
                                0.25*(u(j,M-1)+u(j+1,M-1))*(v(j,M)+v(j,M-1)))/dx
            end do
        
        end subroutine 
        !-----------------------------------------------------------------!
        ! Pressure implementation                                         !
        !-----------------------------------------------------------------!
        subroutine calcpress(P, M, N, dx, dy, maxerror, ustar, vstar, ul, &
                            ur, vb, vt)
            !-------------------------------------------------------------!
            ! Pressure implementation preamble                            !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), intent(in)                            :: dx, dy
            real(WP), dimension(:), intent(in)              :: ul, ur
            real(WP), dimension(:), intent(in)              :: vb, vt
            real(WP), dimension(0:N, 0:M-1), intent(in)     :: ustar
            real(WP), dimension(0:N-1,0:M), intent(in)      :: vstar
            real(WP)                                        :: dx2, dy2, rhs
            real(WP)                                        :: maxerror
            integer                                         :: i, j, gsiter
            real(WP)                                        :: normnew, diffnorm, normerror
            real(WP), dimension(0:N, 0:M), intent(out)      :: P
            real(WP), dimension(0:N, 0:M)                   :: Pold
            !-------------------------------------------------------------!
            ! Domain variables                                            !
            !------------------------------------------------------------!
            gsiter      = 0
            normnew     = 0.0_WP
            diffnorm    = 0.0_WP
            normerror   = 2.0_WP*maxerror
            rhs         = 0.0_WP
            dx2         = dx*dx
            dy2         = dy*dy
            !-------------------------------------------------------------!
            ! Preallocating variables                                     !
            !-------------------------------------------------------------!
            P       = 0.0_WP
            Pold    = 0.0_WP
            !-------------------------------------------------------------!
            ! Gauss Seidel loop                                           !
            !-------------------------------------------------------------!
            do while ((normerror > maxerror .and. gsiter < 6000) .or. (gsiter < 30))
                !---------------------------------------------------------!
                ! Gauss Seidel loop                                       !
                !---------------------------------------------------------!
                normnew     = 0.0_WP
                diffnorm    = 0.0_WP        
                gsiter      = gsiter + 1
                !---------------------------------------------------------!
                ! Lower left                                              !
                !---------------------------------------------------------!
                rhs     = (ustar(1,1)-ul(1))/dx + (vstar(1,1)-vb(1))/dy
                P(1,1)  = 1.0_WP/(1.0_WP/dx2+1.0_WP/dy2)*(P(1,2)/dx2+P(2,1)/dy2-rhs)
                !---------------------------------------------------------!
                ! Lower                                                   !
                !---------------------------------------------------------!
                do i = 2, M-1
                    rhs     = (ustar(1,i)-ustar(1,i-1))/dx + (vstar(1,i)-vb(i))/dy
                    P(1,i)  = 1.0_WP/(2.0_WP/dx2+1.0_WP/dy2)*((P(1,i+1)+ &
                                P(1,i-1))/dx2+P(2,i)/dy2-rhs)
                end do
                !---------------------------------------------------------!
                ! Lower right                                             !
                !---------------------------------------------------------!
                rhs     = (ur(1)-ustar(1,M-1))/dx + (vstar(1,M)-vb(M))/dy
                P(1,M)  = 1.0_WP/(1.0_WP/dx2+1.0_WP/dy2)*(P(1,M-1)/dx2+P(2,M)/dy2-rhs)
                !---------------------------------------------------------!
                ! Left                                                    !
                !---------------------------------------------------------!
                do j = 2, N-1
                    rhs     = (ustar(j,1)-ul(j))/dx + (vstar(j,1)-vstar(j-1,1))/dy
                    P(j,1)  = 1.0_WP/(1.0_WP/dx2+2.0_WP/dy2)*(P(j,2)/dx2+(P(j+1,1)+P(j-1,1))/dy2-rhs)
                    !-----------------------------------------------------!
                    ! Away from boundaries                                !
                    !-----------------------------------------------------!
                    do i = 2, M-1
                        rhs     = (ustar(j,i)-ustar(j,i-1))/dx + (vstar(j,i)-vstar(j-1,i))/dy
                        P(j,i)  = 1.0_WP/(2.0_WP/dx2+2.0_WP/dy2)*((P(j,i+1)+P(j,i-1))/dx2+(P(j+1,i)+P(j-1,i))/dy2-rhs)
                    end do
                    !-----------------------------------------------------!
                    ! Right                                               !
                    !-----------------------------------------------------!
                    rhs     = (ur(j)-ustar(j,M-1))/dx + (vstar(j,M)-vstar(j-1,M))/dy
                    P(j,M)  = 1.0_WP/(1.0_WP/dx2+2.0_WP/dy2)*(P(j,M-1)/dx2+(P(j+1,M)+P(j-1,M))/dy2-rhs)
                end do 
                !---------------------------------------------------------!
                ! Upper left                                              !
                !---------------------------------------------------------!
                rhs     = (ustar(N,1)-ul(N))/dx + (vt(1)-vstar(N-1,1))/dy
                P(N,1)  = 1.0_WP/(1.0_WP/dx2+1.0_WP/dy2)*(P(N,2)/dx2+P(N-1,1)/dy2-rhs)
                !---------------------------------------------------------!
                ! Upper                                                   !
                !---------------------------------------------------------!
                do i = 2, M-1
                    rhs     = (ustar(N,i)-ustar(N,i-1))/dx + (vt(i)-vstar(N-1,i))/dy
                    P(N,i)  = 1.0_WP/(2.0_WP/dx2+1.0_WP/dy2)*((P(N,i+1)+P(N,i-1))/dx2+P(N-1,i)/dy2-rhs)
                end do 
                !---------------------------------------------------------!
                ! Upper right                                             !
                !---------------------------------------------------------!
                rhs     = (ur(N)-ustar(N,M-1))/dx + (vt(M)-vstar(N-1,M))/dy
                P(N,M)  = 1.0_WP/(1.0_WP/dx2+1.0_WP/dy2)*(P(N,M-1)/dx2+P(N-1,M)/dy2-rhs)
                do j = 1, N
                    do i = 1,M
                        normnew     = normnew +  (P(j,i))**(2.0_WP)
                        diffnorm    = diffnorm + (P(j,i)-Pold(j,i))**(2.0_WP)
                    end do
                end do
                normerror   = diffnorm/normnew
                Pold        = P    
            end do
        
            print "(4X, A, ES16.5, 4X, A, I8)", &
                    'GS convergence -->', normerror, &
                    'GS iterations -->', gsiter
            end subroutine
        !-----------------------------------------------------------------!
        ! Divergence calculation                                          !
        !-----------------------------------------------------------------!
        subroutine divergence(maxdiverg, diverg, M, N, u, v, ul, ur, vt,&
                                vb, dx, dy)
            !-------------------------------------------------------------!
            ! Divergence calculation preamble                             !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), intent(in)                            :: dx, dy
            real(WP), dimension(:), intent(in)              :: ul, ur
            real(WP), dimension(:), intent(in)              :: vb, vt
            real(WP), dimension(0:N, 0:M-1), intent(in)     :: u
            real(WP), dimension(0:N-1,0:M), intent(in)      :: v
            real(WP), intent(out)                           :: diverg
            real(WP), intent(out)                           :: maxdiverg
            integer                                         :: i, j
            !-------------------------------------------------------------!
            ! Preallocating divergence                                    !
            !-------------------------------------------------------------!
            diverg      = 0.0_WP
            maxdiverg   = 0.0_wP
            !=============================================================!
            ! Compute cell divergence                                     !
            !=============================================================!
            !-------------------------------------------------------------!
            ! Bottom left                                                 !
            !-------------------------------------------------------------!
            diverg      = (u(1,1)-ul(1))/dx + (v(1,1)-vb(1))/dy
            maxdiverg   = diverg
            !-------------------------------------------------------------!
            ! Across the bottom                                           !
            !-------------------------------------------------------------!
            do i = 2, M-1
                diverg = (u(1,i)-u(1,i-1))/dx + (v(1,i)-vb(i))/dy
                if (abs(diverg)>abs(maxdiverg)) then
                    maxdiverg = diverg
                end if
            end do
            !-------------------------------------------------------------!
            ! Bottom right                                                !
            !-------------------------------------------------------------!
            diverg  = (ur(1)-u(1,M-1))/dx + (v(1,M)-vb(M))/dy
            if (abs(diverg)>abs(maxdiverg)) then
                maxdiverg = diverg
            end if
            !-------------------------------------------------------------!
            ! Middle left                                                 !
            !-------------------------------------------------------------!
            do j = 2, N-1
                diverg  = (u(j,1)-ul(j))/dx + (v(j,1)-v(j-1,1))/dy
                if (abs(diverg)>abs(maxdiverg)) then
                    maxdiverg = diverg
                end if 
                !---------------------------------------------------------!
                ! Across the middle                                       !
                !---------------------------------------------------------!
                do i = 2, M-1
                    diverg  = (u(j,i)-u(j,i-1))/dx + (v(j,i)-v(j-1,i))/dy
                    if (abs(diverg)>abs(maxdiverg)) then
                        maxdiverg = diverg
                    end if
                end do
                !---------------------------------------------------------!
                ! Middle right                                            !
                !---------------------------------------------------------!
                diverg  = (ur(j)-u(j,M-1))/dx + (v(j,M)-v(j-1,M))/dy
                if (abs(diverg)>abs(maxdiverg)) then
                    maxdiverg = diverg
                end if 
            end do
            !-------------------------------------------------------------!
            ! Top left                                                    !
            !-------------------------------------------------------------!
            diverg  = (u(N,1)-ul(N))/dx + (vt(1)-v(N-1,1))/dy
            if (abs(diverg)>abs(maxdiverg)) then
                maxdiverg = diverg
            end if
            !-------------------------------------------------------------!
            ! Across the top                                              !
            !-------------------------------------------------------------!
            do i = 2, M-1
                diverg  = (u(N,i)-u(N,i-1))/dx + (vt(i)-v(N-1,i))/dy
                if (abs(diverg)>abs(maxdiverg)) then
                    maxdiverg = diverg
                end if
            end do
            !-------------------------------------------------------------!
            ! Top right                                                   !
            !-------------------------------------------------------------!
            diverg  = (ur(M)-u(N,M-1))/dx + (vt(M)-v(N-1,M))/dy
            if (abs(diverg)>abs(maxdiverg)) then
                maxdiverg = diverg
            end if 
        end subroutine
        !=================================================================!
        ! Writing u-velocity                                              !
        !=================================================================!
        subroutine u_write(u,M,N,ul,ur)
            !-------------------------------------------------------------!
            ! writing u-velocity preamble                                 !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), dimension(:), intent(in)              :: ul, ur
            real(WP), dimension(0:N, 0:M-1), intent(in)     :: u
            real(WP), dimension(0:N-1,0:M-1)                :: uinterp
            real(WP)                                        :: velcent 
            integer                                         :: i,j
            !-------------------------------------------------------------!
            ! Cell centered u-velocity                                    !
            !-------------------------------------------------------------!
            open(unit=2, file='FORTRAN-data/FORTRAN-64-data/u-velocity.dat')
            11 format(/)
            12 format(32ES25.10)
            !-------------------------------------------------------------!
            ! Looping over domain                                         !
            !-------------------------------------------------------------!
            do j = 1, N
                !---------------------------------------------------------!
                ! Left wall (well next too)                               !
                !---------------------------------------------------------!
                velcent                 = (ul(j)+u(j,1))/2.0_WP
                uinterp(j-1,0)          = velcent
                !---------------------------------------------------------!
                ! Interior domain                                         !
                !---------------------------------------------------------!
                do i  = 2, M-1
                    velcent             = (u(j,i)+u(j,i-1))/2.0_WP
                    uinterp(j-1,i-1)    = velcent
                end do
                !---------------------------------------------------------!
                ! Right wall                                              !
                !---------------------------------------------------------!
                velcent                 = (ur(j)+u(j,M-1))/2.0_WP
                uinterp(j-1,M-1)        = velcent
            end do
            !-------------------------------------------------------------!
            ! Writing cell centered velocities                            !
            !-------------------------------------------------------------!
            do j = 0, N-1
                write(2,12) (uinterp(j,i), i=0,M-1)
            end do
            write(2,11)
        end subroutine
        !=================================================================!
        ! Writing v-velocity                                              !
        !=================================================================!
        subroutine v_write(v,M,N,vt,vb)
            !-------------------------------------------------------------!
            ! writing u-velocity preamble                                 !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), dimension(:), intent(in)              :: vb, vt
            real(WP), dimension(0:N-1,0:M), intent(in)      :: v
            real(WP), dimension(0:N-1,0:M-1)                :: vinterp
            real(WP)                                        :: velcent 
            integer                                         :: i,j
            !-------------------------------------------------------------!
            ! Print variables                                             !
            !-------------------------------------------------------------!
            open(unit=3, file='FORTRAN-data/FORTRAN-64-data/v-velocity.dat')
            13 format(/)
            14 format(32ES25.10)
            !-------------------------------------------------------------!
            ! Bottom wall                                                 !
            !-------------------------------------------------------------!
            do i = 1, M
                velcent         = (v(1,i)+vb(i))/2.0_WP
                vinterp(0,i-1)  = velcent
            end do 
            !-------------------------------------------------------------!
            ! Interior points                                             !
            !-------------------------------------------------------------!
            do j = 2, N-1
                do i = 1, M
                    velcent             = (v(j,i)+v(j-1,i))/2.0_WP
                    vinterp(j-1,i-1)    = velcent
                end do
            end do
            !-------------------------------------------------------------!
            ! Top wall                                                    !
            !-------------------------------------------------------------!
            do i = 1, M
                velcent             = (v(N-1,i)+vt(i))/2.0_WP
                vinterp(N-1,i-1)    = velcent
            end do
            !-------------------------------------------------------------!
            ! Writing solution                                            !
            !-------------------------------------------------------------!
            do j = 0, N-1
                write(3,14) (vinterp(j,i), i=0,M-1)
            end do
            write(3,13)
        end subroutine
        !=================================================================!
        ! Writing Lv term                                                 !
        !=================================================================!
        subroutine Lv_write(Lv,M,N) 
            !-------------------------------------------------------------!
            ! writing Lv preamble                                         !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), dimension(0:N-1,0:M), intent(in)      :: Lv
            integer                                         :: i,j
            !-------------------------------------------------------------!
            ! Print variables                                             !
            !-------------------------------------------------------------!
            open(unit=4, file='FORTRAN-data/FORTRAN-64-data/Lv-term.dat')
            13 format(/)
            14 format(32ES25.10)
            !-------------------------------------------------------------!
            ! Writing solution                                            !
            !-------------------------------------------------------------!
            do j = 1, N-1
                write(4,14) (Lv(j,i), i=1,M)
            end do
            write(4,13)
        end subroutine
        !=================================================================!
        ! Writing Lu term                                                 !
        !=================================================================!
        subroutine Lu_write(Lu,M,N) 
            !-------------------------------------------------------------!
            ! writing Lu preamble                                         !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), dimension(0:N,0:M-1), intent(in)      :: Lu
            integer                                         :: i,j
            !-------------------------------------------------------------!
            ! Print variables                                             !
            !-------------------------------------------------------------!
            open(unit=5, file='FORTRAN-data/FORTRAN-64-data/Lu-term.dat')
            13 format(/)
            14 format(32ES25.10)
            !-------------------------------------------------------------!
            ! Writing solution                                            !
            !-------------------------------------------------------------!
            do j = 1, N
                write(5,14) (Lu(j,i), i=1,M-1)
            end do
            write(5,13)
        end subroutine
        !=================================================================!
        ! Writing Nx term                                                 !
        !=================================================================!
        subroutine Nx_write(Nx,M,N) 
            !-------------------------------------------------------------!
            ! writing Lu preamble                                         !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), dimension(0:N,0:M-1), intent(in)      :: Nx
            integer                                         :: i,j
            !-------------------------------------------------------------!
            ! Print variables                                             !
            !-------------------------------------------------------------!
            open(unit=120, file='FORTRAN-data/FORTRAN-64-data/Nx-term.dat')
            13 format(/)
            14 format(32ES25.10)
            !-------------------------------------------------------------!
            ! Writing solution                                            !
            !-------------------------------------------------------------!
            do j = 1, N
                write(120,14) (Nx(j,i), i=1,M-1)
            end do
            write(120,13)
        end subroutine
        !=================================================================!
        ! Writing Ny term                                                 !
        !=================================================================!
        subroutine Ny_write(Ny,M,N) 
            !-------------------------------------------------------------!
            ! writing Lu preamble                                         !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), dimension(0:N-1,0:M), intent(in)      :: Ny
            integer                                         :: i,j
            !-------------------------------------------------------------!
            ! Print variables                                             !
            !-------------------------------------------------------------!
            open(unit=7, file='FORTRAN-data/FORTRAN-64-data/Ny-term.dat')
            13 format(/)
            14 format(32ES25.10)
            !-------------------------------------------------------------!
            ! Writing solution                                            !
            !-------------------------------------------------------------!
            do j = 1, N-1
                write(7,14) (Ny(j,i), i=1,M)
            end do
            write(7,13)
        end subroutine
        !=================================================================!
        ! Pressure writing                                                !
        !=================================================================!
        subroutine P_write(press,M,N) 
            !-------------------------------------------------------------!
            ! writing Lu preamble                                         !
            !-------------------------------------------------------------!
            integer, intent(in)                             :: M ,N
            real(WP), dimension(0:N,0:M), intent(in)        :: press
            integer                                         :: i,j
            !-------------------------------------------------------------!
            ! Print variables                                             !
            !-------------------------------------------------------------!
            open(unit=121, file='FORTRAN-data/FORTRAN-64-data/P-term.dat')
            13 format(/)
            14 format(32ES25.10)
            !-------------------------------------------------------------!
            ! Writing solution                                            !
            !-------------------------------------------------------------!
            do j = 1, N
                write(121,14) (press(j,i), i=1,M)
            end do
            write(121,13)
        end subroutine

end module ns_lib
