program advection
    !---------------------------------------------------------------------!
    ! Preamble                                                            !
    !---------------------------------------------------------------------!
    use precision_m
    implicit none
    real(WP), parameter             :: pi = 4.0_WP*atan(1.0_WP)
    integer, parameter              :: n = 250
    real(WP), dimension(0:n)        :: x, y
    real(WP), dimension(0:n,0:n)    :: u, unew
    real(WP)                        :: dx, dy, dt
    integer                         :: i, j, k
    real(WP)                        :: del, p
    integer                         :: tf = 20
    real(WP)                        :: C, D
    !---------------------------------------------------------------------!
    ! Domain variables                                                    !
    !---------------------------------------------------------------------!
    C       = 1.0_WP
    D       = 0.5_WP
    dx      = (5.0_WP)/real(n)
    dy      = (5.0_WP)/real(n)
    dt      = 0.01_WP
    !---------------------------------------------------------------------!
    ! Making x and y arrays                                               !
    !---------------------------------------------------------------------!
    x(0:n)  = [(i*dx, i=0, n)]
    y(0:n)  = [(i*dy, i=0, n)]
    !---------------------------------------------------------------------!
    ! printing x and y arrays                                             !
    !---------------------------------------------------------------------!
    100 format (1x, 2(f15.10))
    open(unit=2, file='x.dat')
    open(unit=3, file='y.dat')
    do i = 0, n
        write(2,*) x(i)
        write(3,*) y(i)
    end do
    close(unit=2)
    close(unit=3)
    !---------------------------------------------------------------------!
    ! calculating initial conditions                                      !
    !---------------------------------------------------------------------!
    do j = 0,n
        do i = 0,n
            del = sqrt((x(i)-1.5_WP)**2.0_WP + (y(j)-3.0_WP)**2.0_WP)
            if (del <= 1.0_WP) then
                p = cos(0.5_WP*pi*del)
            else
                p = 0.0_WP
            end if
            u(i,j) = p
            print *, u(i,j)
        end do
    end do
    print *, maxval(u)
    open(unit=1, file='IC.dat')
    !---------------------------------------------------------------------!
    ! writing initial conditions                                          !
    !---------------------------------------------------------------------!
    do, i=0,n
        write(1,*) ( u(i,j), j=0,n )
    end do
    close(unit=1)
    !---------------------------------------------------------------------!
    ! time loop                                                           !
    !---------------------------------------------------------------------!
    unew = u
    do k = 1, int(tf*100)
        !-----------------------------------------------------------------!
        ! looping over the domain                                         !
        !-----------------------------------------------------------------!
        do j = 1, n
            do i = 1, n
                unew(i,j) = u(i,j) - dt*(C*(u(i,j)-u(i-1,j))/dx + D*(u(i,j)-u(i,j-1))/dy)
            end do
        end do
        !-----------------------------------------------------------------!
        ! BC at x = 0                                                     !
        !-----------------------------------------------------------------!
        do j = 0, n
            unew(0,j) = unew(n,j)
        end do
        !-----------------------------------------------------------------!
        ! BC at y = 0  and y = n                                          !
        !-----------------------------------------------------------------!
        do i = 0, n
            unew(i,0) = unew(i,n)
        end do
        u = unew        ! updating solution
    end do
    !---------------------------------------------------------------------!
    ! writing final solution                                              !
    !---------------------------------------------------------------------!
    print *, maxval(u)
    open(unit=4, file='data.dat')
    do, i = 0, n
        write(4,*) ( u(i,j), j=0,n )
    end do
    close(unit=4)
end program advection
